plugins {
	id("net.fabricmc.fabric-loom-remap") version "1.14-SNAPSHOT"
	id("me.modmuss50.mod-publish-plugin") version "0.8.4"
}

version = "${property("mod.version")}+${stonecutter.current.project}"
group = project.property("maven_group").toString()

base {
	archivesName.set(property("archives_base_name").toString())
}

repositories {
	mavenCentral()
	mavenLocal()
}

val configuredVersion = "0.3"

dependencies {
	minecraft("com.mojang:minecraft:${stonecutter.current.project}")
	mappings("net.fabricmc:yarn:${property("deps.yarn_mappings")}:v2")
	modImplementation("net.fabricmc:fabric-loader:${property("deps.fabric_loader")}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${property("deps.fabric_api")}")
	// Configured
	modImplementation(include("de.clickism:configured-core:${configuredVersion}")!!)
	modImplementation(include("de.clickism:configured-yaml:${configuredVersion}")!!)
	modImplementation(include("de.clickism:configured-json:${configuredVersion}")!!)
	modImplementation(include("de.clickism:configured-fabric-command-adapter:${configuredVersion}")!!)
	// Configured Dependency
	implementation(include("org.yaml:snakeyaml:2.0")!!)
}

tasks.processResources {
	val props = mapOf(
		"version" to version,
		"targetVersion" to project.property("mod.mc_version"),
		"minecraftVersion" to stonecutter.current.version,
		"fabricVersion" to project.property("deps.fabric_loader")
	)
	filesMatching("fabric.mod.json") {
		expand(props)
	}
	inputs.properties(props)
}

java {
	val j21 = stonecutter.eval(stonecutter.current.version, ">=1.20.5")
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(if (j21) 21 else 17))
	}
	val javaVersion = if (j21) JavaVersion.VERSION_17 else JavaVersion.VERSION_17
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

tasks.jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}" }
	}
}

publishMods {
	displayName.set("ClickMobs ${property("mod.version")} for Fabric")
	file.set(tasks.remapJar.get().archiveFile)
	version.set(project.version.toString())
	changelog.set(rootProject.file("fabric/CHANGELOG.md").readText())
	type.set(STABLE)
	modLoaders.add("fabric")
	val mcVersions = property("mod.target_mc_versions").toString().split(',')
	modrinth {
		accessToken.set(System.getenv("MODRINTH_TOKEN"))
		projectId.set("tRdRT5jS")
		requires("fabric-api")
		minecraftVersions.addAll(mcVersions)
	}
	curseforge {
		accessToken.set(System.getenv("CURSEFORGE_TOKEN"))
		projectId.set("1179556")
		clientRequired.set(false)
		serverRequired.set(true)
		requires("fabric-api")
		minecraftVersions.addAll(mcVersions)
	}
}

loom {
	runConfigs.all {
		ideConfigGenerated(true)
		runDir = "../../run"
	}
}